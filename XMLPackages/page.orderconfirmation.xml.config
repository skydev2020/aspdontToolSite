<?xml version="1.0" standalone="yes" ?>
<package displayname="Order Confirmation Page" version="2.1" debug="false">

	<!-- ###################################################################################################### -->
	<!-- Notes: expects runtime param of OrderNumber to be passed in by storefront code                         -->
	<!-- ###################################################################################################### -->

	<query name="Orders" rowElementName="Order">
		<sql>
			<![CDATA[
                select *, coalesce(nullif(SUBSTRING(ShippingMethod,0,CHARINDEX('|',ShippingMethod)), ''), ShippingMethod) ShippingMethodName from Orders with (NOLOCK) where OrderNumber=@OrderNumber
            ]]>
		</sql>
		<queryparam paramname="@OrderNumber" paramtype="runtime" requestparamname="OrderNumber" sqlDataType="int" defvalue="0"  validationpattern="" />
	</query>
	<query name="Downloads" rowElementName="Download">
		<sql>
			<![CDATA[
                SELECT OrderNumber, CustomerId, OrderedProductVariantName, OrderedProductName, DownloadStatus, DownloadLocation, DownloadValidDays, DownloadCategory, DownloadReleasedOn, CreatedOn 
								FROM Orders_ShoppingCart with (NOLOCK) WHERE IsDownload=1 and OrderNumber=@OrderNumber
            ]]>
		</sql>
		<queryparam paramname="@OrderNumber" paramtype="runtime" requestparamname="OrderNumber" sqlDataType="int" defvalue="0"  validationpattern="" />
	</query>

	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" xmlns:msxsl="urn:schemas-microsoft-com:xslt" exclude-result-prefixes="aspdnsf msxsl">
			<xsl:output method="html" omit-xml-declaration="yes" indent="no"  encoding="utf-8"/>

			<xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
			<xsl:param name="OrderNumber">
				<xsl:value-of select="/root/Runtime/OrderNumber" />
			</xsl:param>
			<xsl:param name="StoreURL">
				<xsl:value-of select="/root/Runtime/StoreUrl" />
			</xsl:param>
			<xsl:param name="DownloadsUrl">
				<xsl:value-of select="aspdnsf:BuildRoute('Index','Downloads')" />
			</xsl:param>

			<xsl:template match="/">
				<xsl:param name="PaymentMethod">
					<xsl:value-of select="/root/Orders/Order/PaymentMethod" />
				</xsl:param>
				<xsl:param name="CustomerID">
					<xsl:value-of select="/root/Orders/Order/CustomerID" />
				</xsl:param>
				<xsl:param name="PMCleaned">
					<xsl:value-of select="aspdnsf:CleanPaymentMethod($PaymentMethod)" />
				</xsl:param>
				<xsl:param name="ReceiptUrlParameters">
					<param name="OrderNumber" value="{$OrderNumber}" />
				</xsl:param>
				<xsl:param name="ReceiptURL">
					<xsl:value-of select="aspdnsf:BuildRoute('index', 'receipt', msxsl:node-set($ReceiptUrlParameters))" />
				</xsl:param>

				<div class="page-row orderconfirmation-page">
					<xsl:choose>
						<xsl:when test="$PMCleaned='CREDITCARD'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.2',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='MICROPAY'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.4',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-micropay-balance">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.9',$LocaleSetting)" disable-output-escaping="yes"/>
								<xsl:value-of select="aspdnsf:MicroPayBalance()"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='PURCHASEORDER'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.5',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-order-instructions">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.10',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='PAYPALEXPRESS'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.6',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='REQUESTQUOTE'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.7',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-order-instructions">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.11',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='CHECKBYMAIL'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.8',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-order-instructions">
								<xsl:value-of select="aspdnsf:Topic('CHECKINSTRUCTIONS')" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='COD'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.23',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-order-instructions">
								<xsl:value-of select="aspdnsf:Topic('CODINSTRUCTIONS')" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
						<xsl:when test="$PMCleaned='AMAZONPAYMENTS'">
							<div class="orderconfirmation-received">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.amazon.orderreceieved',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-thanks">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.3',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
						</xsl:when>
					</xsl:choose>

					<div class="orderconfirmation-print">
						<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.12',$LocaleSetting)" disable-output-escaping="yes"/>
					</div>

					<div class="orderconfirmation-ordernumber">
						<span>
							<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.13',$LocaleSetting)" disable-output-escaping="yes"/>
						</span>
						<xsl:value-of select="$OrderNumber" />
					</div>

					<div class="orderconfirmation-customerid">
						<span>
							<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.14',$LocaleSetting)" disable-output-escaping="yes"/>
						</span>
						<xsl:value-of select="$CustomerID" />
					</div>

					<xsl:choose>
						<xsl:when test="$PMCleaned='CREDITCARD' and /root/Orders/Order/Email!=''">
							<div class="orderconfirmation-email">
								<span>
									<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.15',$LocaleSetting)" disable-output-escaping="yes"/>
								</span>
								<xsl:value-of select="/root/Orders/Order/Email"/>
							</div>
						</xsl:when>
					</xsl:choose>

					<xsl:if test="/root/Orders/Order/BillingAddress1 != '' or /root/Orders/Order/ShippingAddress1 != ''">
						<div class="order-confirmation-shipping-method order-confirmation-addresses">
							<xsl:if test="/root/Orders/Order/BillingAddress1 != ''">
								<div class="order-confirmation-billing-address">
									<h4 class="order-confirmation-billing-address-header">
										<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.billingaddress.header', $LocaleSetting)" disable-output-escaping="yes"/>
									</h4>
									<div>
										<xsl:value-of select="concat(/root/Orders/Order/BillingFirstName, ' ', /root/Orders/Order/BillingLastName)" />
									</div>
									<div>
										<xsl:value-of select="/root/Orders/Order/BillingAddress1" />
									</div>
									<xsl:if test="/root/Orders/Order/BillingAddress2">
										<div>
											<xsl:value-of select="/root/Orders/Order/BillingAddress2" />
										</div>
									</xsl:if>
									<div>
										<xsl:value-of select="concat(/root/Orders/Order/BillingCity, ', ', /root/Orders/Order/BillingState, ' ', /root/Orders/Order/BillingZip)" />
									</div>
									<div>
										<xsl:value-of select="/root/Orders/Order/BillingCountry" />
									</div>
								</div>
							</xsl:if>
							<xsl:if test="/root/Orders/Order/ShippingAddress1 != ''">
								<div class="order-confirmation-shipping-address">
									<h4 class="order-confirmation-shipping-address-header">
										<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.shippingaddress.header', $LocaleSetting)" disable-output-escaping="yes"/>
									</h4>
									<div>
										<xsl:value-of select="concat(/root/Orders/Order/ShippingFirstName, ' ', /root/Orders/Order/ShippingLastName)" />
									</div>
									<div>
										<xsl:value-of select="/root/Orders/Order/ShippingAddress1" />
									</div>
									<xsl:if test="/root/Orders/Order/ShippingAddress2">
										<div>
											<xsl:value-of select="/root/Orders/Order/ShippingAddress2" />
										</div>
									</xsl:if>
									<div>
										<xsl:value-of select="concat(/root/Orders/Order/ShippingCity, ', ', /root/Orders/Order/ShippingState, ' ', /root/Orders/Order/ShippingZip)" />
									</div>
									<div>
										<xsl:value-of select="/root/Orders/Order/ShippingCountry" />
									</div>
								</div>
							</xsl:if>
						</div>
					</xsl:if>
					<xsl:if test="/root/Orders/Order/ShippingMethod != ''">
						<div>
							<div class="order-confirmation-shipping-method">
								<h4 class="order-confirmation-shipping-method-header">
									<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.shippingmethod.header', $LocaleSetting)" disable-output-escaping="yes"/>
								</h4>
								<div>
									<xsl:value-of select="/root/Orders/Order/ShippingMethodName" />
								</div>
							</div>
						</div>
					</xsl:if>

					<xsl:if test="aspdnsf:AppConfigBool('ForceSignoutOnOrderCompletion') = 'false' and /root/System/CustomerIsRegistered='true'">
						<div class="orderconfirmation-receipt">
							<a class="order-confirmation-receipt-button btn btn-default" target="_blank" href="{$ReceiptURL}">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.view.receipt.button.text', $LocaleSetting)" disable-output-escaping="yes"/>
							</a>

							<a class="orderconfirmation-receipt-button btn btn-default" target="_blank" href="{concat($ReceiptURL, '&amp;print=true')}">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.print.receipt.button.text', $LocaleSetting)" disable-output-escaping="yes"/>
							</a>
						</div>
					</xsl:if>
					<xsl:if test="count(/root/Downloads/Download) &gt; 0">
						<xsl:if test="count(/root/Downloads/Download[DownloadStatus = 0]) &gt; 0">
							<div class="orderconfirmation-pendingdownloads">
								<xsl:value-of select="aspdnsf:StrFormatStringresource('orderconfirmation.aspx.25',$DownloadsUrl,'')" disable-output-escaping="yes"/>
							</div>
						</xsl:if>
						<xsl:if test="count(/root/Downloads/Download[DownloadStatus = 1]) &gt; 0">
							<div class="orderconfirmation-availabledownloads">
								<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.26',$LocaleSetting)" disable-output-escaping="yes"/>
							</div>
							<div class="orderconfirmation-downloadlink">
								<xsl:value-of select="aspdnsf:StrFormatStringresource('orderconfirmation.aspx.24',$DownloadsUrl,'')" disable-output-escaping="yes"/>
							</div>
						</xsl:if>
					</xsl:if>
				</div>
			</xsl:template>
		</xsl:stylesheet>
	</PackageTransform>

	<SearchEngineSettings>
		<SectionTitle actionType="transform">
			<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf">
				<xsl:output method="html" omit-xml-declaration="yes" />
				<xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
				<xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
				<xsl:param name="Prompt">
					<xsl:value-of select="aspdnsf:StringResource('orderconfirmation.aspx.1', $LocaleSetting)" />
				</xsl:param>
				<xsl:template match="/">
					<xsl:value-of select="$Prompt" />
				</xsl:template>
			</xsl:stylesheet>
		</SectionTitle>
	</SearchEngineSettings>
</package>
