<?xml version="1.0" standalone="yes" ?>
<!-- This XML package displays sub-entities only (no products) in either a list or a grid.  -->
<!-- If displaying in a grid, the number of columns is determined by the 'Column Width' attribute of the entity.  -->
<package version="2.1" displayname="Sub-Entities In A List (No Products)" debug="false" includeentityhelper="true">

	<PackageTransform>

		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
			<xsl:output method="html" omit-xml-declaration="yes" />

			<xsl:variable name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
			<xsl:variable name="ShowSubcatsInGrid" select="aspdnsf:AppConfig('ShowSubcatsInGrid')" />
			<xsl:variable name="EntityName" select="/root/Runtime/EntityName" />
			<xsl:variable name="EntityID" select="/root/Runtime/EntityID" />

			<xsl:variable name="CurrentEntity" select="/root/EntityHelpers/*[name()=$EntityName]/descendant::Entity[EntityID=$EntityID]" />

			<!-- Determine the right CSS class to use on grid items on this page based on the number of columns that will display -->
			<xsl:variable name="ColumnCount" select="number($CurrentEntity/ColWidth)" />
			<xsl:variable name="GridItemCssClassName">
				<xsl:choose>
					<xsl:when test="$ColumnCount = 1">col-sm-12</xsl:when>
					<xsl:when test="$ColumnCount = 2">col-sm-6</xsl:when>
					<xsl:when test="$ColumnCount = 3">col-sm-4</xsl:when>
					<xsl:when test="$ColumnCount = 4">col-sm-3</xsl:when>
					<xsl:when test="$ColumnCount = 5">col-sm-5ths</xsl:when>
					<xsl:when test="$ColumnCount = 6">col-sm-2</xsl:when>
					<xsl:otherwise>col-sm-3</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>

			<xsl:variable name="GridItemSmallestClassName">
				<xsl:choose>
					<xsl:when test="$ColumnCount mod 2 = 0">col-xs-6</xsl:when>
					<xsl:otherwise>col-sm-12</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>

			<xsl:variable name="BaseURL" select="aspdnsf:EntityLink(/root/Runtime/EntityID, $CurrentEntity/SEName, /root/Runtime/EntityName, 0)" />

			<xsl:variable name="CurrentPage">
				<xsl:choose>
					<xsl:when test="/root/QueryString/pagenum">
						<xsl:value-of select="/root/QueryString/pagenum" />
					</xsl:when>
					<xsl:otherwise>1</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>

			<!-- This is the main template for this package.  It runs regardless of whether or not this entity contains any sub-entities or products. -->
			<xsl:template match="/">
				<div class="page-wrap entity-page">
					<h1 class="entity-page-header">
						<xsl:value-of select="aspdnsf:GetMLValue($CurrentEntity/Name)" disable-output-escaping="yes" />
					</h1>
					<div class="page-row entity-description-wrap">
						<xsl:value-of select="aspdnsf:EntityPageHeaderDescription($EntityName, $EntityID)" disable-output-escaping="yes" />
					</div>
					<xsl:choose>
						<xsl:when test="count($CurrentEntity/Entity) = 0">
							<xsl:value-of select="aspdnsf:Topic(concat('empty', /root/Runtime/EntityName, 'text'))" disable-output-escaping="yes" />
						</xsl:when>
						<xsl:when test="$ShowSubcatsInGrid = 'true'">
							<div class="sub-entity-grid">
								<div class="row sub-entity-row">
									<xsl:apply-templates select="$CurrentEntity/Entity" mode="grid" />
								</div>
							</div>
						</xsl:when>
						<xsl:otherwise>
							<ul class="sub-entity-list">
								<xsl:apply-templates select="$CurrentEntity/Entity" mode="list" />
							</ul>
						</xsl:otherwise>
					</xsl:choose>
				</div>
			</xsl:template>

			<!-- Runs once for each sub-entity if ShowSubCatsInGrid AppConfig = true -->
			<xsl:template match="Entity" mode="grid">
				<xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)" />
				<xsl:variable name="AltText">
					<xsl:choose>
						<xsl:when test="aspdnsf:GetMLValue(SEAltText)=''">
							<xsl:value-of select="$Name" />
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="aspdnsf:GetMLValue(SEAltText)" />
						</xsl:otherwise>
					</xsl:choose>
				</xsl:variable>
				<xsl:variable name="URL">
					<xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" />
				</xsl:variable>

				<!-- Start a new row if this one is full -->
				<xsl:if test="position() mod $ColumnCount = 1 and position() &gt; 1">
					<xsl:variable name="NewRowHtml">
						<xsl:text>&lt;/div&gt;&lt;div class="row sub-entity-row"&gt;</xsl:text>
					</xsl:variable>
					<xsl:value-of select="$NewRowHtml" disable-output-escaping="yes" />
				</xsl:if>

				<!-- Assign the class determined above for the grid -->
				<div class="{$GridItemCssClassName} {$GridItemSmallestClassName} sub-entity-grid-item grid-item">
					<div class="grid-item-inner">
						<a href="{$URL}" class="sub-entity-link">
							<div class="sub-entity-image-wrap grid-item-image-wrap">
								<img src="{aspdnsf:ImageUrl(EntityID, $EntityName, 'icon', 0)}" alt="{$AltText}" class="img-responsive img-thumbnail sub-entity-grid-image grid-item-image" />
							</div>
							<div class="sub-entity-name-wrap grid-item-name-wrap">
								<xsl:value-of select="$Name" disable-output-escaping="yes"/>
							</div>
						</a>
					</div>
				</div>
				<!-- Break to two columns if we can on the smallest viewport -->
				<xsl:if test="$ColumnCount mod 2 = 0 and position() mod 2 = 0">
					<div class="clearfix visible-xs sub-entity-extra-small-clear"></div>
				</xsl:if>
			</xsl:template>

			<!-- Runs once for each sub-entity if ShowSubCatsInGrid AppConfig = false -->
			<xsl:template match="Entity" mode="list">
				<xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)" />
				<xsl:variable name="URL">
					<xsl:value-of select="aspdnsf:EntityLink(EntityID, SEName, $EntityName, 0, '')" />
				</xsl:variable>
				<li class="sub-entity-list-item">
					<a href="{$URL}" class="sub-entity-name-link">
						<xsl:value-of select="$Name" disable-output-escaping="yes"/>
					</a>
				</li>
			</xsl:template>

		</xsl:stylesheet>
	</PackageTransform>
</package>
