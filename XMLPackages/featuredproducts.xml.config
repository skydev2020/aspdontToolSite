<?xml version="1.0" standalone="yes" ?>
<package version="2.1" displayname="Featured Products" debug="false" includeentityhelper="false">
	<query name="FeaturedProducts" rowElementName="Product" >
		<sql>
			<![CDATA[
			
				exec aspdnsf_GetFeaturedProducts @NumHomePageFeaturedProducts, @CustomerLevelID, @InventoryFilter, @StoreID, @FilterProduct					
            ]]>
		</sql>
		<queryparam paramname="@NumHomePageFeaturedProducts"	paramtype="appconfig"	requestparamname="FeaturedProducts.NumberOfItems"				sqlDataType="int"	defvalue="0"	validationpattern="" />
		<queryparam paramname="@CustomerLevelID"				paramtype="system"		requestparamname="CustomerLevelID"								sqlDataType="int"	defvalue="0"	validationpattern="" />
		<queryparam paramname="@InventoryFilter"				paramtype="appconfig"	requestparamname="HideProductsWithLessThanThisInventoryLevel"	sqlDataType="int"	defvalue="0"	validationpattern="" />
		<queryparam paramname="@StoreID"						paramtype="runtime"		requestparamname="StoreID"										sqlDataType="int"	defvalue="1"	validationpattern="" />
		<queryparam paramname="@FilterProduct"					paramtype="runtime"		requestparamname="FilterProduct"								sqlDataType="bit"	defvalue="0"	validationpattern="" />
	</query>

	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
						xmlns:aspdnsf="urn:aspdnsf"
						exclude-result-prefixes="aspdnsf" >
			<xsl:output method="html" omit-xml-declaration="yes" />
			<xsl:variable name="FeaturedProductCount" select="count(/root/FeaturedProducts/Product)" />
			<xsl:variable name="ColumnCount" select="aspdnsf:AppConfig('FeaturedProducts.NumberOfColumns')" />
			<xsl:variable name="GridItemCssClassName">
				<xsl:choose>
					<xsl:when test="$ColumnCount = 1">col-sm-12</xsl:when>
					<xsl:when test="$ColumnCount = 2">col-sm-6</xsl:when>
					<xsl:when test="$ColumnCount = 3">col-sm-4</xsl:when>
					<xsl:when test="$ColumnCount = 4">col-sm-3</xsl:when>
					<xsl:when test="$ColumnCount = 5">col-sm-5ths</xsl:when>
					<xsl:when test="$ColumnCount = 6">col-sm-2</xsl:when>
					<xsl:otherwise>col-sm-3</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>

			<xsl:variable name="GridItemSmallestClassName">
				<xsl:choose>
					<xsl:when test="$ColumnCount mod 2 = 0">col-xs-6</xsl:when>
					<xsl:otherwise>col-sm-12</xsl:otherwise>
				</xsl:choose>
			</xsl:variable>
			<xsl:variable name="ProductIdsOnPage">
				<xsl:for-each select="/root/FeaturedProducts/Product/ProductID">
					<xsl:value-of select="." />
					<xsl:if test="position() != last()">
						<xsl:text>,</xsl:text>
					</xsl:if>
				</xsl:for-each>
			</xsl:variable>
			<xsl:variable name="AvailablePromotions" select="aspdnsf:XmlPackageAsXml('availablePromotions.xml.config', concat('productIdList=', $ProductIdsOnPage))/root/Promotions" />

			<xsl:template match="/" >
				<xsl:if test="count(/root/FeaturedProducts/Product) &gt; 0">
					<div class="home-featured-products">
						<h2 class="group-header home-featured-products-header">
							<xsl:value-of select="aspdnsf:StringResource('Header.FeaturedItemsHeader')" disable-output-escaping="yes"/>
						</h2>
						<div class="home-featured-products">
							<div class="row featured-item-row">
								<xsl:apply-templates select="/root/FeaturedProducts/Product" />
							</div>
						</div>
					</div>
				</xsl:if>
			</xsl:template>

			<xsl:template match="Product">
				<xsl:variable name="Name" select="aspdnsf:GetMLValue(Name)"></xsl:variable>
				<xsl:variable name="SalesPromptName" select="aspdnsf:GetMLValue(SalesPromptName)"></xsl:variable>
				<xsl:variable name="SEAltText" select="aspdnsf:GetMLValue(SEAltText)"></xsl:variable>
				<xsl:variable name="AltText">
					<xsl:choose>
						<xsl:when test="$SEAltText=''">
							<xsl:value-of select="$Name" />
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="$SEAltText" />
						</xsl:otherwise>
					</xsl:choose>
				</xsl:variable>
				<xsl:variable name="ProductID" select="ProductID" />
				<xsl:variable name="IsRecurring" select="HasRecurring" />
				<xsl:variable name="ProductLink" select="aspdnsf:ProductLink(ProductID, SEName, 0)" />
				<xsl:variable name="ImageUrl" select="aspdnsf:ImageUrl(ProductID, 'product', 'icon', 0)" />

				<xsl:if test="position() mod $ColumnCount = 1 and position() &gt; 1">
					<xsl:variable name="NewRowHtml">
						<xsl:text>&lt;/div&gt;&lt;div class="row featured-item-row"&gt;</xsl:text>
					</xsl:variable>
					<xsl:value-of select="$NewRowHtml" disable-output-escaping="yes" />
				</xsl:if>

				<!-- Assign the class determined above for the grid -->
				<div class="{$GridItemCssClassName} {$GridItemSmallestClassName} product-grid-item">
					<div class="grid-item-inner">
						<a href="{$ProductLink}" class="featured-item-link">
							<div class="featured-item-image-wrap grid-item-image-wrap">
								<img src="{$ImageUrl}" alt="{$AltText}" class="grid-item-image product-image featured-item-image" />
							</div>
							<div class="featured-item-name-wrap grid-item-name-wrap">
								<xsl:value-of select="$Name" disable-output-escaping="yes" />
							</div>
						</a>
						<xsl:if test="aspdnsf:AppConfigBool('FeaturedProducts.ShowPrice') = 'true'">
							<div class="grid-item-price-wrap">
								<xsl:value-of select="aspdnsf:GetVariantPrice(VariantID, HidePriceUntilCart, Price, SalePrice, ExtendedPrice, Points, SalesPromptName)" disable-output-escaping="yes"/>
							</div>
						</xsl:if>
						<xsl:if test="aspdnsf:AppConfigBool('DisplayOutOfStockProducts') = 'true'">
							<div class="featured-item-stock-wrap">
								<xsl:value-of select="aspdnsf:DisplayProductStockHint(ProductID,'Entity')" disable-output-escaping="yes" />
							</div>
						</xsl:if>
						<xsl:if test="$IsRecurring = 'True'">
							<div class="recurring-call-to-action">
								<xsl:value-of select="aspdnsf:StringResource('product.CallToAction.IsRecurring')" disable-output-escaping="yes" />
							</div>
						</xsl:if>
						<xsl:if test="aspdnsf:AppConfigBool('FeaturedProducts.ShowAddToCartForm') = 'true'" >
							<div class="featuredCartWrap">
								<xsl:value-of select="aspdnsf:SimpleAddToCartForm(ProductID, 1)" disable-output-escaping="yes"/>
							</div>
						</xsl:if>
						<xsl:apply-templates select="$AvailablePromotions/Promotion[ProductId=$ProductID]" />
					</div>
				</div>
				<!-- Break to two columns if we can on the smallest viewport -->
				<xsl:if test="$ColumnCount mod 2 = 0 and position() mod 2 = 0">
					<div class="clearfix visible-xs entity-product-extra-small-clear"></div>
				</xsl:if>
			</xsl:template>

			<xsl:template match="Promotion">
				<div class="promotion-call-to-action">
					<xsl:value-of select="CallToAction" disable-output-escaping="yes" />
				</div>
			</xsl:template>

		</xsl:stylesheet>
	</PackageTransform>
</package>
