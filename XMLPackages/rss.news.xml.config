<?xml version="1.0" standalone="yes" ?>
<package version="2.1" displayname="RSS Feed - News" debug="false" allowengine="true">
	<query name="Articles" rowElementName="Article">
		<sql>
			<![CDATA[
                select top @Limit NewsID, NewsGUID, Headline, NewsCopy, CreatedOn
                from dbo.news with (nolock) 
                where expireson > getdate() and year(createdon) in (year(getdate()), year(getdate())-1) and Deleted = 0
                order by createdon desc
            ]]>
		</sql>
		<querystringreplace replaceTag="@Limit" replacetype="request" replaceparamname="limit" defvalue="5" validationpattern="^\d{1,10}$" />
	</query>

	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:msxsl="urn:schemas-microsoft-com:xslt" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf msxsl">
			<xsl:output method="xml" omit-xml-declaration="no" />

			<xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
			<xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />

			<xsl:template match="/">
				<rss version="2.0">
					<channel>
						<generator>AspDotNetStorefront</generator>
						<title>AspDotNetStorefront - News Items</title>
						<link>http://www.aspdotnetstorefront.com/rssfeed.aspx</link>
						<description>AspDotNetStorefront - RSS Feed (News)</description>
						<date>
							<xsl:value-of select="/root/System/CurrentDateTime"/>
						</date>
						<xsl:apply-templates select="/root/Articles/Article" />
					</channel>
				</rss>
			</xsl:template>

			<xsl:template match="Article">
				<xsl:param name="StoreURL">
					<xsl:value-of select="/root/System/StoreUrl"/>
				</xsl:param>
				<xsl:param name="pHeadline">
					<xsl:choose>
						<xsl:when test="count(Headline/ml/locale[@name=$LocaleSetting])!=0">
							<xsl:value-of select="Headline/ml/locale[@name=$LocaleSetting]"/>
						</xsl:when>
						<xsl:when test="count(Headline/ml/locale[@name=$WebConfigLocaleSetting]) !=0 ">
							<xsl:value-of select="Headline/ml/locale[@name=$WebConfigLocaleSetting]"/>
						</xsl:when>
						<xsl:when test="count(Headline/ml)=0">
							<xsl:value-of select="Headline"/>
						</xsl:when>
					</xsl:choose>
				</xsl:param>
				<!--Build up a node with the info we'll need for the route -->
				<xsl:variable name="NewsInfo">
					<param name="id" value="{ProductID}" />
				</xsl:variable>
				<xsl:variable name="NewsLink" select="aspdnsf:BuildRoute('index', 'news', msxsl:node-set($NewsInfo))" />

				<item>
					<title>
						<xsl:value-of select="$pHeadline"/>
					</title>
					<description>
						<xsl:value-of select="aspdnsf:Decode(NewsCopy)"/>
					</description>
					<date>
						<xsl:value-of select="aspdnsf:GetNativeShortDateString(CreatedOn)" />
					</date>
					<link>
						<xsl:value-of select="concat($StoreURL, $NewsLink)"/>
					</link>
					<image></image>
				</item>

			</xsl:template>
		</xsl:stylesheet>
	</PackageTransform>
</package>
