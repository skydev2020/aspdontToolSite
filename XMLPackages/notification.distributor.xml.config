<?xml version="1.0" encoding="utf-8" ?>
<package displayname="Distributor Notification Email" version="2.1" debug="false">
	<query name="Order" rowElementName="OrderInfo">
		<sql>
			<![CDATA[
            select o.* ,
                a.AddressIDs,
                case when a.Downloads = a.ItemCount then 1 else 0 end allDownloads,
                case when a.FreeShipping = a.ItemCount then 1 else 0 end allFreeShipping,
                case when a.SystemProducts = a.ItemCount then 1 else 0 end allSystemproducts,
                case a.AddressIDs when 1 then 0 else 1 end multiship
            From orders o with (NOLOCK) 
                join (select ordernumber , count(distinct sc.ShippingAddressID) AddressIDs, sum(sc.IsDownload) Downloads, sum(sc.FreeShipping) FreeShipping, sum(sc.isSystem) SystemProducts, count(*) ItemCount From Orders_ShoppingCart sc with (NOLOCK)  where ordernumber = @ordernum group by ordernumber ) a on o.ordernumber = a.ordernumber
            where o.ordernumber = @ordernum
            ]]>
		</sql>
		<queryparam paramname="@ordernum" paramtype="runtime" requestparamname="ordernumber" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
		<queryparam paramname="@distributorid" paramtype="runtime" requestparamname="DistributorID" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
	</query>
	<query name="OrderItems" rowElementName="lineitem">
		<sql>
			<![CDATA[
            select od.*
            From dbo.Orders_ShoppingCart od with (NOLOCK) 
            where od.ordernumber = @ordernum and od.DistributorID = @distributorid
            ]]>
		</sql>
		<queryparam paramname="@ordernum" paramtype="runtime" requestparamname="ordernumber" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
		<queryparam paramname="@distributorid" paramtype="runtime" requestparamname="DistributorID" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
	</query>
	<query name="KitOrderItems" rowElementName="kititem">
		<sql>
			<![CDATA[
            select kc.* 
            From dbo.Orders_KitCart kc with (NOLOCK) 
                join dbo.Orders_ShoppingCart od on kc.ShoppingCartRecID = od.ShoppingCartRecID
            where kc.ordernumber = @ordernum and od.DistributorID = @distributorid
            ]]>
		</sql>
		<queryparam paramname="@ordernum" paramtype="runtime" requestparamname="ordernumber" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
		<queryparam paramname="@distributorid" paramtype="runtime" requestparamname="DistributorID" defvalue="0" sqlDataType="int" validationpattern="^\d{1,9}$"/>
	</query>
	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
			<xsl:output method="html" omit-xml-declaration="yes" encoding="ISO-8859-1" />
			<xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
			<xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
			<xsl:param name="ordernumber" select="/root/Runtime/ordernumber"></xsl:param>
			<xsl:param name="DefaultSizeOptionPrompt" select="aspdnsf:StringResource('AppConfig.SizeOptionPrompt')" />
			<xsl:param name="DefaultColorOptionPrompt" select="aspdnsf:StringResource('AppConfig.ColorOptionPrompt')" />
			<xsl:param name="DefaultTextOptionPrompt" select="aspdnsf:StringResource('common.cs.70')" />
			<xsl:param name="OrderTotal" select="/root/Order/OrderInfo/OrderTotal" />
			<xsl:param name="ShowDistributorNotificationPriceInfo" select="aspdnsf:AppConfigBool('ShowDistributorNotificationPriceInfo') = 'true'" />
			<xsl:variable name="StoreURL" select="/root/Runtime/StoreUrl" />
			<xsl:variable name="StoreId" select="root/Order/OrderInfo/StoreID" />

			<xsl:variable name="fontFamily">
				<xsl:value-of select="aspdnsf:AppConfig('email.font.family')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="fontSize">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.font.size')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="fontLineHeight">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.line.height')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="fontColor">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.font.color')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="emailFrame">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.main.background')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="emailBackground">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.header.background')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="storeLogo">
				<xsl:value-of select="aspdnsf:AppConfig('email.styles.logo.image')" disable-output-escaping="yes"/>
			</xsl:variable>
			<xsl:variable name="emailHeader" select="aspdnsf:StrToLower(aspdnsf:AppConfig('email.header.logo'))"/>
			<xsl:variable name="storeName" select="aspdnsf:AppConfig($StoreId, 'StoreName')"/>


			<xsl:template match="/">
				<html>
					<head>
						<title>
							<xsl:value-of select="aspdnsf:AppConfig('StoreName')" disable-output-escaping="yes" /> - DISTRIBUTOR NOTIFICATION ORDER NUMBER <xsl:value-of select="$ordernumber" />
						</title>
					</head>
					<body>
						<!--[if mso]>
					<style type="text/css">
						.body-text {
							font-family: Arial, sans-serif;
						}
					</style>
					<![endif]-->

						<div style="{$fontFamily}{$fontColor}text-align:center;">
							<p>&#0160;</p>
							<xsl:choose>
								<xsl:when test="$emailHeader = 'true'">
									<a href="{/root/System/StoreUrl}" title="{$storeName} Logo" target="_blank">
										<img src="{/root/System/StoreUrl}/images/{$storeLogo}" alt="{$storeName}" />
									</a>
									<h3>Distributor Notification</h3>
								</xsl:when>
								<xsl:otherwise>
									<h2 style="{$fontFamily}">
										<xsl:value-of select="aspdnsf:AppConfig('StoreName')" disable-output-escaping="yes" />
									</h2>
									<h3>Distributor Notification</h3>
								</xsl:otherwise>
							</xsl:choose>
							<p>&#0160;</p>
						</div>
						<table width="100%">
							<tr>
								<td width="10%"></td>
								<td width="80%" style="{$fontFamily}{$fontColor}{$fontSize}{$fontLineHeight}">
									<xsl:apply-templates select="/root/Order/OrderInfo" />
									<p>
										<b>Packing List</b>
									</p>
									<table border="1" cellspacing="0" cellpadding="4">
										<tr>
											<td valign="top" align="left">
												<b>Item</b>
											</td>
											<xsl:if test="$ShowDistributorNotificationPriceInfo">
												<td valign="top" align="center">
													<b>Line Item Total</b>
												</td>
											</xsl:if>
											<td valign="top" align="left">
												<b>Mfg Part #</b>
											</td>
											<td valign="top" align="center">
												<b>Qty</b>
											</td>
										</tr>
										<!-- ITEM_START -->
										<xsl:apply-templates select="/root/OrderItems/lineitem" />
										<!-- ITEM_END -->
									</table>
									<xsl:if test="$ShowDistributorNotificationPriceInfo">
										<p>
											<table cellspacing="0" cellpadding="4">
												<tr>
													<td valign="top">
														<b>Order Total(Includes tax,shipping,and discounts):</b>
													</td>
													<td valign="top" align="center">
														<xsl:value-of select="aspdnsf:FormatCurrency($OrderTotal)" />
													</td>
												</tr>
											</table>
										</p>
									</xsl:if>
									<br />
									<div>
										<strong>
											<a href="{$StoreURL}">
												<xsl:value-of select="aspdnsf:AppConfig($StoreId, 'StoreName')" disable-output-escaping="yes" />
											</a>
										</strong>
									</div>
								</td>
								<td width="10%"></td>
							</tr>
						</table>

					</body>
				</html>
			</xsl:template>

			<xsl:template match="OrderInfo">
				<xsl:param name="OrderNotes" select="OrderNotes" />
				<xsl:param name="CustomerOrderNotes">
					<xsl:choose>
						<xsl:when test="$OrderNotes=''">
							None
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="$OrderNotes" />
						</xsl:otherwise>
					</xsl:choose>
				</xsl:param>
				<xsl:param name="CSNotes" select="CustomerServiceNotes" />
				<xsl:param name="CustomerServiceNotes">
					<xsl:choose>
						<xsl:when test="$CSNotes=''">
							None
						</xsl:when>
						<xsl:otherwise>
							<xsl:value-of select="$CSNotes" />
						</xsl:otherwise>
					</xsl:choose>
				</xsl:param>
				<p>
					Order Number:&#0160; <xsl:value-of select="$ordernumber" /><br />
					Order Date: <xsl:value-of select="OrderDate" />
				</p>
				<table>
					<tr>
						<td style="{$fontFamily}{$fontSize}{$fontLineHeight}">
							<p>
								<b>Shipping Info</b>
							</p>
							<xsl:if test="/root/Order/OrderInfo/multiship=0">
								<p>
									<xsl:value-of select="concat(ShippingFirstName, ' ', ShippingLastName)" /><br />
									<xsl:value-of select="ShippingAddress1" /><br />
									<xsl:if test="ShippingAddress2!=''">
										<xsl:value-of select="ShippingAddress2" />
										<br />
									</xsl:if>
									<xsl:if test="ShippingSuite!=''">
										<xsl:value-of select="ShippingSuite" />
										<br />
									</xsl:if>
									<xsl:value-of select="ShippingCity" />, <xsl:value-of select="ShippingState" />&#0160;<xsl:value-of select="ShippingZip" /><br />
									<xsl:value-of select="ShippingCountry" /><br />
									<xsl:value-of select="ShippingPhone" />
									<p>
										E-Mail: <xsl:value-of select="Email" />
									</p>
									<p>
										<b>
											<font color="red">Shipping Method:</font>&#0160;<xsl:value-of select="ShippingMethod" />
										</b>
									</p>
								</p>
							</xsl:if>
							<xsl:if test="/root/Order/OrderInfo/multiship=1">
								<b>
									<xsl:value-of select="aspdnsf:StringResource('checkoutshippingmult.aspx.26')" disable-output-escaping="yes" />
								</b>
							</xsl:if>
						</td>
						<td width="20">&#0160;</td>
						<td>
						</td>
					</tr>
				</table>
				<p>
					Notes From Customer: <xsl:value-of select="$CustomerOrderNotes" />
				</p>
				<p>
					Notes From Customer Service: <xsl:value-of select="$CustomerServiceNotes" />
				</p>
			</xsl:template>
			<xsl:template match="lineitem">
				<xsl:param name="pSizeOptionPrompt" select="aspdnsf:GetMLValue(SizeOptionPrompt)" />
				<xsl:param name="pColorOptionPrompt" select="aspdnsf:GetMLValue(ColorOptionPrompt)" />
				<xsl:param name="pTextOptionPrompt" select="aspdnsf:GetMLValue(TextOptionPrompt)" />
				<xsl:param name="LineItemRecID" select="ShoppingCartRecID" />
				<tr>
					<td valign="top" align="left" style="{$fontFamily}{$fontSize}{$fontLineHeight}">
						<xsl:value-of select="aspdnsf:GetMLValue(OrderedProductName)" />
						<xsl:if test="OrderedProductVariantName!=''">
							-<xsl:value-of select="aspdnsf:GetMLValue(OrderedProductVariantName)" />
						</xsl:if>
						<xsl:if test="ChosenSize!=''">
							&#0160;&#0160;&#0160;&#0160;<xsl:choose>
								<xsl:when test="$pSizeOptionPrompt=''">
									<xsl:value-of select="$DefaultSizeOptionPrompt" />
								</xsl:when>
								<xsl:otherwise>
									<xsl:value-of select="$pSizeOptionPrompt" />
								</xsl:otherwise>
							</xsl:choose>:&#0160;<xsl:value-of select="ChosenSize" />
						</xsl:if>
						<xsl:if test="ChosenColor!=''">
							&#0160;&#0160;&#0160;&#0160;<xsl:choose>
								<xsl:when test="$pColorOptionPrompt=''">
									<xsl:value-of select="$DefaultColorOptionPrompt" />
								</xsl:when>
								<xsl:otherwise>
									<xsl:value-of select="$pColorOptionPrompt" />
								</xsl:otherwise>
							</xsl:choose>:&#0160;<xsl:value-of select="ChosenColor" />
						</xsl:if>
						<xsl:if test="TextOption!=''">
							&#0160;&#0160;&#0160;&#0160;<xsl:choose>
								<xsl:when test="$pTextOptionPrompt=''">
									<xsl:value-of select="$DefaultTextOptionPrompt" />
								</xsl:when>
								<xsl:otherwise>
									<xsl:value-of select="$pTextOptionPrompt" />
								</xsl:otherwise>
							</xsl:choose>:&#0160;<xsl:value-of select="TextOption" />
						</xsl:if>
						<xsl:if test="IsAKit=1">
							<xsl:apply-templates select="/root/KitOrderItems/kititem[ShoppingCartRecID=$LineItemRecID]" />
						</xsl:if>
						<xsl:if test="/root/Order/OrderInfo/multiship=1">
							<xsl:value-of select="aspdnsf:StringResource('shoppingcart.cs.87')" disable-output-escaping="yes" />
							<xsl:value-of select="aspdnsf:DisplayAddressString(ShippingAddressID)" disable-output-escaping="yes" />
							<div>
								<xsl:value-of select="aspdnsf:StringResource('order.cs.68')" disable-output-escaping="yes" />&#0160;
								<xsl:value-of select="ShippingMethod" disable-output-escaping="yes" />
							</div>
						</xsl:if>
					</td>
					<xsl:if test="$ShowDistributorNotificationPriceInfo">
						<td valign="top" align="center" style="{$fontFamily}{$fontSize}{$fontLineHeight}">
							<xsl:value-of select="aspdnsf:FormatCurrency(OrderedProductPrice)" />
						</td>
					</xsl:if>
					<td valign="top" align="left">
						<xsl:value-of select="OrderedProductManufacturerPartNumber" />&#0160;
					</td>
					<td valign="top" align="center">
						<xsl:value-of select="Quantity" />
					</td>
				</tr>
			</xsl:template>
			<xsl:template match="kititem">
				<xsl:param name="pKitItemName" select="aspdnsf:GetMLValue(KitItemName)"></xsl:param>
				<xsl:param name="pProductVariantName" select="aspdnsf:GetMLValue(ProductVariantName)"></xsl:param>
				&#0160;&#0160;&#0160;&#0160;- (<xsl:value-of select="Quantity" />)&#0160;<xsl:value-of select="$pKitItemName" disable-output-escaping="yes"/>
				<xsl:if test="KitGroupTypeID=4 or KitGroupTypeID=5">
					:&#0160;&#0160;<b>
						<xsl:value-of select="TextOption" />
					</b>
				</xsl:if>
				<xsl:if test="KitGroupTypeID=6">
					<img src="{/root/System/StoreUrl}{TextOption}"  border="0" />
				</xsl:if>
			</xsl:template>
		</xsl:stylesheet>
	</PackageTransform>
</package>
