<?xml version="1.0" standalone="yes" ?>
<!-- ##################################################################################	-->
<!-- Copyright AspDotNetStorefront.com. All Rights Reserved.							-->
<!-- http://www.aspdotnetstorefront.com													-->
<!-- For details on this license please visit the product homepage at the URL above.	-->
<!-- THE ABOVE NOTICE MUST REMAIN INTACT.												-->
<!-- ##################################################################################	-->
<package version="2.1" displayname="Entity Menu" debug="false" includeentityhelper="true">
	<PackageTransform>
		<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	  xmlns:msxsl="urn:schemas-microsoft-com:xslt"
	  xmlns:aspdnsf="urn:aspdnsf"
	  exclude-result-prefixes="aspdnsf msxsl">
			<xsl:output method="html" omit-xml-declaration="yes"/>

			<xsl:variable name="MenuEntityType" select="aspdnsf:StrCapitalize(/root/Runtime/entitytype)" />
			<xsl:variable name="CurrentEntityType" select="aspdnsf:StrCapitalize(/root/Runtime/PageType)" />
			<xsl:variable name="CurrentEntityID" select="/root/Runtime/PageID" />
			<xsl:variable name="MaxMenuSize" select="aspdnsf:AppConfig('MaxMenuSize')" />
			<xsl:variable name="MenuEntities" select="/root/EntityHelpers/*[name() = $MenuEntityType]/Entity" />

			<xsl:template match="/">
				<ul class="dropdown-menu">
					<xsl:choose>
						<xsl:when test="$MaxMenuSize = 0">
							<xsl:apply-templates select="$MenuEntities[position()]" />
						</xsl:when>
						<xsl:otherwise>
							<xsl:apply-templates select="$MenuEntities[position() &lt;= $MaxMenuSize]" />
						</xsl:otherwise>
					</xsl:choose>
					<xsl:if test="(count($MenuEntities) &gt; $MaxMenuSize) and ($MaxMenuSize &gt; 0)">
						<li class="divider"></li>
						<li class="menu-item-more">
							<!--Build up a node with the info we'll need for the route -->
							<xsl:variable name="PackageInfo">
								<param name="EntityType" value="{$MenuEntityType}" />
							</xsl:variable>

							<a href="{aspdnsf:BuildRoute('index', 'entity', msxsl:node-set($PackageInfo))}" class="menu-item-more">
								<xsl:value-of select="aspdnsf:StringResource('common.cs.62')" disable-output-escaping="yes" />
							</a>
						</li>
					</xsl:if>
				</ul>
			</xsl:template>

			<xsl:template match="Entity">
				<xsl:variable name="CurrentDepth" select="count(ancestor::Entity)+1" />
				<li>
					<a href="{aspdnsf:EntityLink(EntityID, SEName, $MenuEntityType, 0)}">
						<xsl:if test="($CurrentEntityType = $MenuEntityType) and (EntityID = $CurrentEntityID or descendant::Entity/EntityID = $CurrentEntityID)">
							<xsl:attribute name="class">selected</xsl:attribute>
						</xsl:if>
						<xsl:value-of select="aspdnsf:GetMLValue(Name)"/>
					</a>
				</li>
			</xsl:template>

		</xsl:stylesheet>
	</PackageTransform>
</package>
