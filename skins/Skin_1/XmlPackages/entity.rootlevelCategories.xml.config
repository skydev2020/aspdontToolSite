<?xml version="1.0" standalone="yes" ?>
<package version="2.1" displayname="Entity Root Level Grid" debug="false" includeentityhelper="true">

	<!-- ###################################################################################################### -->
	<!-- Copyright AspDotNetStorefront.com, 1995-2011.  All Rights Reserved.					                -->
	<!-- http://www.aspdotnetstorefront.com														                -->
	<!-- For details on this license please visit  the product homepage at the URL above.		                -->
	<!-- THE ABOVE NOTICE MUST REMAIN INTACT.                                                                   -->
	<!--                                                                                                        -->
	<!-- Notes: expects param of Entity to be passed in by querystring                                          -->
	<!-- e-entity.rootlevel.aspx?entity=Category                                                                -->
    <!-- the querystring param name must be all lower case and the values must be one of the following values   -->
    <!-- (case sensitive: Category, Section, Manufacturer, Library)                                             -->
	<!-- ###################################################################################################### -->
<query name="Categories" rowElementName="Category">
        <sql>
            <![CDATA[
                select * from category where Published =1 and Deleted = 0 and ParentCategoryID = 0 and categoryid <> @catid order by DisplayOrder ASC
            ]]>
        </sql>
        <queryparam paramname="@CatID"  paramtype="runtime"   requestparamname="EntityID"  sqlDataType="int"     defvalue="0"      validationpattern="" />
	</query>
<query name="Products" rowElementName="Product">
        <sql>
            <![CDATA[
                select p.ProductID, pc.CategoryID ,p.SEName from Product p inner join ProductCategory pc on pc.ProductID = p.ProductID where p.Published = 1 and p.Deleted =0

            ]]>
        </sql>
	</query>

    <PackageTransform>
    
        <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf" exclude-result-prefixes="aspdnsf">
          <xsl:output method="html" omit-xml-declaration="yes" />

            <xsl:param name="EntityName">Category</xsl:param>

            <xsl:param name="CurrentEntityID">
			<xsl:value-of select="/root/Runtime/EntityID"/>
		</xsl:param>
            
            <xsl:param name="ShowInGrid">true</xsl:param>
            <xsl:param name="GridCols">4</xsl:param>

            <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
            <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />

            <xsl:template match="/">
                <xsl:choose>
	               	<xsl:when test="$ShowInGrid = 'true'">
    	            <table border="0" cellpadding="0" cellspacing="4" width="100%">
						<xsl:apply-templates select="/root/Categories/Category"/>
            	    </table>
					</xsl:when>
                	<xsl:otherwise>
						<xsl:apply-templates select="/root/Categories/Category" />
                	</xsl:otherwise>
                </xsl:choose>
            </xsl:template>

            <xsl:template match="Category">
                <xsl:param name="scName">
                    <xsl:choose>
                        <xsl:when test="count(Name/ml/locale[@name=$LocaleSetting])!=0">
                            <xsl:value-of select="Name/ml/locale[@name=$LocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml/locale[@name=$WebConfigLocaleSetting]) !=0 ">
                            <xsl:value-of select="Name/ml/locale[@name=$WebConfigLocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml)=0">
                            <xsl:value-of select="Name"/>
                        </xsl:when>
                    </xsl:choose>
                </xsl:param>


                <xsl:choose>
                	<xsl:when test="$ShowInGrid = 'true'">
                            <xsl:if test="position() mod $GridCols = 1">
                                <tr>
                                 <xsl:for-each select=". | following-sibling::*[position() &lt; $GridCols]">
                                    <xsl:call-template name="EntityCell">
                                    </xsl:call-template>
                                 </xsl:for-each>
                                </tr>
                                <tr>
                                    <td height="10" colspan="{$GridCols}">&#0160;</td>
                                </tr>
                            </xsl:if>
                    </xsl:when>
                	<xsl:otherwise>
                        <p align="left">
                            &#0160;&#0160;&#0160;<img border="0" src="{concat('App_Themes/Skin_', aspdnsf:SkinID(), '/images/redarrow.gif')}"></img>&#0160;
                            <a href="{aspdnsf:EntityLink(CategoryID, SEName, $EntityName, 0, '')}" class="category-title">
                                <xsl:value-of select="$scName" disable-output-escaping="yes"/>
                            </a>
                        </p>
                    </xsl:otherwise>
                </xsl:choose>
            </xsl:template>

            <xsl:template name="EntityCell">
                <xsl:param name="scName">
                    <xsl:choose>
                        <xsl:when test="count(Name/ml/locale[@name=$LocaleSetting])!=0">
                            <xsl:value-of select="Name/ml/locale[@name=$LocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml/locale[@name=$WebConfigLocaleSetting]) !=0 ">
                            <xsl:value-of select="Name/ml/locale[@name=$WebConfigLocaleSetting]"/>
                        </xsl:when>
                        <xsl:when test="count(Name/ml)=0">
                            <xsl:value-of select="Name"/>
                        </xsl:when>
                    </xsl:choose>
                </xsl:param>
                <xsl:param name="AltText">
                    <xsl:choose>
                        <xsl:when test="aspdnsf:GetMLValue(SEAltText)=''"><xsl:value-of select="$scName" /></xsl:when>
                        <xsl:otherwise><xsl:value-of select="aspdnsf:GetMLValue(SEAltText)" /></xsl:otherwise>
                    </xsl:choose>
                </xsl:param>
			<xsl:param name="CatID" select="CategoryID"/>
                <xsl:param name="URL">
				<xsl:choose>
					<xsl:when test="count(/root/Products/Product[CategoryID = $CatID]) = 1">
						<xsl:value-of select="aspdnsf:ProductLink(/root/Products/Product[CategoryID = $CatID]/ProductID, /root/Products/Product[CategoryID = $CatID]/SEName,'')" />
					</xsl:when>
					<xsl:otherwise>
						<xsl:value-of select="aspdnsf:EntityLink(CategoryID, SEName, $EntityName, 0, '')" />
					</xsl:otherwise>
				</xsl:choose>
			</xsl:param>
                <td align="center" style="width: 25%;">
                    <a href="{$URL}">
                        <xsl:value-of select="aspdnsf:LookupEntityImage(CategoryID, $EntityName, 'icon', 0, $AltText)" disable-output-escaping="yes" />
                    </a>
                    <br/>
                    <a href="{$URL}" class="category-title">
                        <xsl:value-of select="$scName" disable-output-escaping="yes"/>
                    </a>
                </td>
            </xsl:template>
        </xsl:stylesheet>
    </PackageTransform>

	<SearchEngineSettings>
		<SectionTitle actionType="transform">
            <xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:aspdnsf="urn:aspdnsf">
              <xsl:output method="html" omit-xml-declaration="yes" />
    	        <xsl:param name="LocaleSetting" select="/root/Runtime/LocaleSetting" />
	            <xsl:param name="WebConfigLocaleSetting" select="/root/Runtime/WebConfigLocaleSetting" />
	            <xsl:param name="EntityName"><xsl:value-of select="/root/Runtime/EntityName" /></xsl:param>
	            <xsl:param name="EntityACName">AppConfig.<xsl:value-of select="$EntityName"/>PromptPlural</xsl:param>
	            <xsl:param name="EntityDisplayName"><xsl:value-of select="aspdnsf:StringResource($EntityACName, $LocaleSetting)" /></xsl:param>
              <xsl:template match="/">
                All <xsl:value-of select="$EntityDisplayName" />
              </xsl:template>
            </xsl:stylesheet>
		</SectionTitle>
	</SearchEngineSettings>

</package>
